

set(data_types "double;long;int;float")#dim4;dim32")
set(omp_reduction "reduction(+:out);reduction(min:out);reduction(max:out)")
set(oper_util "ADD;MIN;MAX")
set(reduce_policies "_REDUCE_ATOMICALLY_AFTER_WARP;_REDUCE_ATOMICALLY_AFTER_TEAM;_REDUCE_LEAGUE_VIA_ATOMICS_WITH_OFFSET;_REDUCE_LEAGUE_VIA_LARGE_BUFFER")
set(policy_name "AAW;AAT;LAWO;LLB")


foreach(dt IN LISTS data_types)
  foreach(ompR op IN ZIP_LISTS omp_reduction oper_util)
    foreach(policy pn IN ZIP_LISTS reduce_policies policy_name)
      set(REDUCTION_TYPE ${dt})
      set(OMP_REDUCTION ${ompR})
      set(OPER_UTIL ${op})
      set(REDUCE_POLICY ${policy})
      set(DEST_SRC_FILE "${CMAKE_CURRENT_BINARY_DIR}/ompNew_${dt}_${pn}_${op}.cpp")
      set(DEST_BIN_FILE "ompNew_${dt}_${pn}_${op}.exe")
      message(STATUS "DT=${dt} Reduction Type=${ompR} operation=${op} ${DEST_SRC_FILE}")
      configure_file(ompNew.cpp ${DEST_SRC_FILE})
      add_executable(${DEST_BIN_FILE} ${DEST_SRC_FILE})
      target_compile_options(${DEST_BIN_FILE} PUBLIC "SHELL:${OPENMP_CMP_OPTIONS}")
      target_link_options(${DEST_BIN_FILE} PUBLIC "SHELL:${OPENMP_CMP_OPTIONS}")
      target_compile_definitions(${DEST_BIN_FILE} PRIVATE "__OPEN_MP__")
      if ( DEFINED EXTRA_OMP_LIBS )
        target_link_libraries(${DEST_BIN_FILE} PRIVATE ${EXTRA_OMP_LIBS})
      endif()
      target_include_directories(${DEST_BIN_FILE} PRIVATE "${CMAKE_SOURCE_DIR}/include")
      target_include_directories(${DEST_BIN_FILE} PRIVATE "${CMAKE_CURRENT_SOURCE_DIR}/")
  endforeach()
  endforeach()
endforeach()

